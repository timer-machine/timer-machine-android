// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.kapt) apply false
    alias(libs.plugins.kotlin.parcelize) apply false
    alias(libs.plugins.hilt) apply false
    alias(libs.plugins.versions)
    alias(libs.plugins.dependencyGuard) apply false
    alias(libs.plugins.gms) apply false
    alias(libs.plugins.secrets) apply false
    alias(libs.plugins.detekt)
}

subprojects {
    apply from: rootProject.file('detekt.gradle')

    afterEvaluate {
        if (project.plugins.hasPlugin(libs.plugins.android.application.get().pluginId) ||
                project.plugins.hasPlugin(libs.plugins.android.library.get().pluginId)
        ) {
            android {
                compileSdkVersion libs.versions.compileSdk.get() as Integer

                defaultConfig {
                    minSdk libs.versions.minSdk.get() as Integer
                    targetSdk libs.versions.targetSdk.get() as Integer
                    vectorDrawables.useSupportLibrary true
                }

                compileOptions {
                    coreLibraryDesugaringEnabled true
                    sourceCompatibility = JavaVersion.VERSION_17
                    targetCompatibility = JavaVersion.VERSION_17
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_17.toString()

                    freeCompilerArgs += [
                            "-P",
                            "plugin:androidx.compose.compiler.plugins.kotlin:stabilityConfigurationPath=" +
                                    project.rootDir.absolutePath + "/compose_compiler_config.conf"
                    ]
                }

                buildFeatures {
                    viewBinding true
                }
            }

            dependencies {
                coreLibraryDesugaring libs.desugarJdkLibs
            }
        }
    }
}

tasks.named("dependencyUpdates").configure {
    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA']
                .any { keyword -> version.toUpperCase().contains(keyword) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
    }

    rejectVersionIf {
        !it.currentVersion.contains(".") ||
                (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion))
    }
}
